#!/usr/bin/perl

use strict;
use warnings;
use Fatal qw(mkdir open);
use OA::Indicator;
use OA::Indicator::DB;

my $oai = new OA::Indicator (verbose => 1);
my ($year, $run_type, $data_type, @desc) = @ARGV;
if (!$oai->valid_year ($year)) {
    &usage ($oai);
}
my $dir = "/var/lib/oa-indicator/$year";
if (!-e $dir) {
    die ("no data for year: $year ($dir)\n");
}
if ($run_type !~ m/^(devel|test|prod)$/) {
    &usage ($oai, "invalid run-type: $run_type");
}
if ($data_type !~ m/^(begin|bfi|bib|doaj|mxd|romeo|end)$/) {
    &usage ($oai, "invalid data-type: $data_type");
}
if ($data_type !~ m/^(begin|end)$/) {
    $dir .= '/' . $data_type;
    if (!-e $dir) {
        &usage ($oai, "no data for type: $data_type ($dir)");
    }
    $oai->log_file ("$dir/load.log");
}
my $oaiDB = new OA::Indicator::DB ($year, $run_type);
if ($data_type eq 'begin') {
    $oaiDB->create (join (' ', @desc));
    $oaiDB->segment_start ('load');
    $oai->log_main ('i', "starting a new load for $year, $run_type run #" . $oaiDB->run());
    exit (0);
}
if ($data_type eq 'end') {
#   FIX - success and errors should be much more informaed
    $oaiDB->open ();
    $oaiDB->segment_end ('load', 'success', '');
    exit (0);
}
my $db = $oaiDB->open ();
$oai->log_main ('i', "loading $data_type records, see $dir/load.log for details");
my $handler;
if ($data_type eq 'bfi') {
    require OA::Indicator::DB::BFI;
    $handler = new OA::Indicator::DB::BFI ($db, $oai);
} elsif ($data_type eq 'bib') {
    require OA::Indicator::DB::Bib;
    $handler = new OA::Indicator::DB::Bib ($db, $oai);
} elsif ($data_type eq 'doaj') {
    require OA::Indicator::DB::DOAJ;
    $handler = new OA::Indicator::DB::DOAJ ($db, $oai);
} elsif ($data_type eq 'mxd') {
    require OA::Indicator::DB::MXD;
    $handler = new OA::Indicator::DB::MXD ($db, $oai);
} elsif ($data_type eq 'romeo') {
    require OA::Indicator::DB::Romeo;
    $handler = new OA::Indicator::DB::Romeo ($db, $oai);
}
$handler->create ();
$handler->load ($year);
exit (0);

sub usage
{
    my ($oai, $msg) = @_;

    if ($msg) {
        warn ($msg . "\n");
    }
    die ("usage: oa-indicator-harvest-load <year> <run-type> begin [description]\n" .
         "       oa-indicator-harvest-load <year> <run-type> <data-type>\n" .
         "       oa-indicator-harvest-load <year> <run-type> end\n" .
         '       year:      ' . $oai->valid_year_range . "\n" .
         '       run-type:  devel | test | prod' . "\n" .
         '       data-type: bfi | bib | doaj | mxd | romeo' . "\n");
}
